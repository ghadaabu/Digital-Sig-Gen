#include  <msp430xG46x.h>

;------------------------------------------------------------------------------- 
;          Macro define Lcd_cmd 
;-------------------------------------------------------------------------------           

Lcd_cmd      MACRO command                  ; LCD command MACRO 
             Delay #del5                    ; dellay of 5msec
             mov.b command, &P5OUT      
             call #Lcd_strobe      
             ENDM   
;------------------------------------------------------------------------------- 
;          Macro define Delay - causes delay for LCD SetUp
;-------------------------------------------------------------------------------           
             
Delay        MACRO del_num  ; dellay MACRO (relevant to the LCD settings and command)
             LOCAL  LOOP
             mov.w del_num, R11
LOOP         dec.w R11
             NOP
             NOP
             jnz LOOP
             ENDM
;*******************************************************************************                                 
             ORG 1700h
             
del15        EQU 3000     ; dellay of 15msec
del5         EQU 1000     ; dellay of 5msec
del02        EQU 40       ; dellay of 0.2msec          

s1  DB '1.Instructions  '  
s2  DB '2.Modulation Fre'
fre1  DB '1KHz            '
fre2  DB '2KHz            '
fre4  DB '4KHz            '
fre6  DB '6KHz            '
quitStr DB 'Press 5 to Quit '
;*******************************************************************************                    

;===============================================================================
             MODULE  lcd
             PUBLIC  LCD_SETUP,Lcd_strobe, Lcd_data, PRINT_STR, firstMenu,PrintChoice,PrintQuitMsg
             EXTERN  Delay1s
;===============================================================================
;-------------------------------------------------------------------------------
            RSEG    CODE                    ; Assemble to Flash memory
;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
; LCD Set Up
;-------------------------------------------------------------------------------
LCD_SETUP    bic.b #0xe0,&P3OUT             ; p3.7=E=0,p3.6=R/W= 0, p3.5=RS=0
             Delay #del15                   ; dellay of 15msec
             mov.b #0x3f, &P5OUT
             call #Lcd_strobe
             Delay #del5                    ; dellay of 5msec
             mov.b #0x3f, &P5OUT
             call #Lcd_strobe
             Delay #del02                   ; dellay of 0.2msec
             mov.b #0x3f, &P5OUT
             call #Lcd_strobe             
             Lcd_cmd #0x3c
             Lcd_cmd #0x0f
             Lcd_cmd #0x01
             Lcd_cmd #0x06
             Lcd_cmd #0x80
             Lcd_cmd #0x02
             Lcd_cmd #0x0C
             ret    
             

;-------------------------------------------------------------------------------
; LCD Strobe
;-------------------------------------------------------------------------------
Lcd_strobe   bis.b #0x80,&P3OUT             
             NOP
             NOP
             bic.b #0x80,&P3OUT
             ret
             

;-------------------------------------------------------------------------------
; LCD Data
;-------------------------------------------------------------------------------
Lcd_data     Delay #del5                    ; dellay of 5msec      
             mov.b #0x00,&P5OUT      
             bis.b #0x20,&P3OUT
             mov.b R14, &P5OUT      
             call #Lcd_strobe
             bic.b #0x20,&P3OUT      
             ret 
;-------------------------------------------------------------------------------
; PRINT_STR - prints = 16 bit string on the LCD
;-------------------------------------------------------------------------------             
PRINT_STR    mov #16,R6           
PRINT        mov.b @R5,R14
             call #Lcd_data 
             inc R5
             dec R6
             jnz PRINT 
             ret

;-------------------------------------------------------------------------------
; Prints the main menu with the first two options
;-------------------------------------------------------------------------------             
firstMenu    Lcd_cmd #0x01
             mov #s1,R5
             call #PRINT_STR
             Lcd_cmd #0xC0
             mov #s2,R5
             call #PRINT_STR
             ret
            
;-------------------------------------------------------------------------------
; Prints user choice
;-------------------------------------------------------------------------------             
PrintChoice  cmp   #0x01,R14
             jne   check2
             mov #fre1,R5
             jmp printt              
check2       cmp   #0x02,R14
             jne   check4
             mov #fre2,R5
             jmp printt
check4       cmp   #0x04,R14
             jne   check6
             mov #fre4,R5
             jmp printt
check6       mov #fre6,R5              
printt       call #PRINT_STR
             call #Delay1s
             call #Delay1s
             ret
             
;-------------------------------------------------------------------------------
; Prints Quit from Action MSG
;-------------------------------------------------------------------------------             
PrintQuitMsg Lcd_cmd  #0x01 
             Lcd_cmd  #0x02
             mov #quitStr,R5  
             call #PRINT_STR
             ret
             

       ENDMOD 
;===============================================================================
                
       END
           